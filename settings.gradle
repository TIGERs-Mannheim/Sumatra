/*
 * Copyright (c) 2009 - 2021, DHBW Mannheim - TIGERs Mannheim
 */

pluginManagement {
    repositories {
        maven {
            url 'https://nexus.tigers-mannheim.de/repository/gradle-plugin-portal/'
        }
        gradlePluginPortal()
    }
}

plugins {
    id("org.gradle.toolchains.foojay-resolver-convention") version "0.5.0"
    id "com.gradle.enterprise" version "3.13.2"
}

rootProject.name = 'sumatra'

// Include libraries (based on Gradle) during development:
//includeBuild("../instanceables") {
//    dependencySubstitution {
//        substitute module("com.github.g3force:instanceables") with project(":")
//    }
//}

buildCache {
    remote(HttpBuildCache) {
        url = 'https://build-cache.tigers-mannheim.de/cache/'
        credentials {
            username = System.getenv("GRADLE_BUILD_CACHE_USERNAME") ?: "read"
            password = System.getenv("GRADLE_BUILD_CACHE_PASSWORD") ?: "I\"\"H=*#X11he9fW-=U\"k*80+lR-'uy7="
        }
        push = (System.getenv("GRADLE_BUILD_CACHE_USERNAME") != null)
    }
}

file('modules').eachDir { dir ->
    include dir.name
    project(":${dir.name}").projectDir = dir
}

gradleEnterprise {
    buildScan {
        termsOfServiceUrl = "https://gradle.com/terms-of-service"
        termsOfServiceAgree = "yes"
        uploadInBackground = false
    }
}
