plugins {
    id 'jacoco'
}

dependencies {
    // Support junit 5
    testImplementation(platform(libs.org.junit.bom))
    testImplementation('org.junit.jupiter:junit-jupiter')
    testRuntimeOnly('org.junit.platform:junit-platform-launcher')

    // Add common test tools
    testImplementation(libs.org.assertj.assertj.core)
    testImplementation(libs.org.mockito.mockito.all)
}

test {
    useJUnitPlatform()
    reports {
        junitXml.required = true
        html.required = true
    }
    exclude '**/*IntegrationTest.class'
}

jacoco {
    toolVersion = libs.versions.org.jacoco.get()
}

jacocoTestReport {
    reports {
        xml.required = true
        csv.required = false
        html.required = false
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ['**/proto/**/*'])
        }))
    }
}
